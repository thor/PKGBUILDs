# Maintainer: Thor K. H. <thor alfakrÃ¸ll roht dott no>
# Contributor: Tim Besard <tim $dot$ besard $at$ gmail $dot$ com>
# Contributor: Jelle van der Waa <jellevdwaa @ gmail.com>
# Contributor: Pieter Kokx <pieter $at$ kokx $dot$ .nl>

pkgname=whatpulse
pkgver=2.5
pkgrel=3
pkgdesc="Measures your keyboard, mouse and application usage, network traffic and uptime."
arch=('i686' 'x86_64')
url=http://www.whatpulse.org
license=(custom:whatpulse_tos)
install="$pkgname.install"
depends=(qt4)
optdepends=(
    'systray: any systray, like gnome-panel'
    'libpcap: for capturing network statistics'
)
source=('whatpulse.desktop')
sha256sums=('9ad42960f0a8b538f1d98fea1ced9b2a97ef59aadca50d0382efdb71434572b7')
source_i686=("http://whatpulse.org/files/whatpulse-linux-32bit-$pkgver.tar.gz")
sha256sums_i686=('c864498d5ce7acb71d5683e3de639df362443e56082a72e3c21f93444d0164a5')
source_x86_64=("http://whatpulse.org/files/whatpulse-linux-64bit-$pkgver.tar.gz")
sha256sums_x86_64=('9d8c635a11709e6536fb0943901b5b9e31efaf768504dea625e2efaaa4f78e33')

package() {
    cd $srcdir/
    # Install the binary
    mkdir -p ${pkgdir}/usr/bin
    install -m0755 whatpulse ${pkgdir}/usr/bin/
    # Install the freedesktop shortcut
    mkdir -p ${pkgdir}/usr/share/applications
    install -m0644 whatpulse.desktop ${pkgdir}/usr/share/applications/
    
    mkdir -p ${pkgdir}/etc/udev/rules.d/
    cat >${pkgdir}/etc/udev/rules.d/99-whatpulse-input.rules <<__EOF__
KERNEL=="event*", NAME="input/%k", MODE="640", GROUP="input"
__EOF__
}

# Maintainer: Thor M. K. Høgås <thor alfakrøll roht dott no>
pkgname=libfyba
pkgver=4.1.1
pkgrel=1
pkgdesc="Library for reading and writing to the national Norwegian geographical SOSI-format"
arch=('i686' 'x86_64')
url="https://github.com/kartverket/fyba"
license=('custom')
depends=('gcc-libs')
source=("https://github.com/kartverket/fyba/archive/4.1.1.zip")
sha512sums=('88d086d6adfb4c0cdbe4e68f241f6e35e519bf107c35bde01daf526b9777778423db7f2e0bc424de62d4b7ecc70fcf95ffac1c8af8f02bf2840ef7b409ddd527')

build() {
  cd "fyba-${pkgver}"
  sed '1,/LICENSE/d' README > LICENSE
  autoreconf --force --install
  ./configure --prefix=/usr
  make
}

package() {
  install -D -m644 "fyba-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cd "fyba-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
